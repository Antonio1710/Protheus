#Include "Protheus.ch"

#DEFINE STR0001 FWI18NLang("MATR120","STR0001",1)
#DEFINE STR0002 FWI18NLang("MATR120","STR0002",2)
#DEFINE STR0003 FWI18NLang("MATR120","STR0003",3)
#DEFINE STR0004 FWI18NLang("MATR120","STR0004",4)
#DEFINE STR0005 FWI18NLang("MATR120","STR0005",5)
#DEFINE STR0006 FWI18NLang("MATR120","STR0006",6)
#DEFINE STR0007 FWI18NLang("MATR120","STR0007",7)
#DEFINE STR0008 FWI18NLang("MATR120","STR0008",8)
#DEFINE STR0009 FWI18NLang("MATR120","STR0009",9)
#DEFINE STR0010 FWI18NLang("MATR120","STR0010",10)
#DEFINE STR0011 FWI18NLang("MATR120","STR0011",11)
#DEFINE STR0012 FWI18NLang("MATR120","STR0012",12)
#DEFINE STR0013 FWI18NLang("MATR120","STR0013",13)
#DEFINE STR0014 FWI18NLang("MATR120","STR0014",14)
#DEFINE STR0015 FWI18NLang("MATR120","STR0015",15)
#DEFINE STR0016 FWI18NLang("MATR120","STR0016",16)
#DEFINE STR0017 FWI18NLang("MATR120","STR0017",17)
#DEFINE STR0018 FWI18NLang("MATR120","STR0018",18)
#DEFINE STR0019 FWI18NLang("MATR120","STR0019",19)
#DEFINE STR0020 FWI18NLang("MATR120","STR0020",20)
#DEFINE STR0021 FWI18NLang("MATR120","STR0021",21)
#DEFINE STR0022 FWI18NLang("MATR120","STR0022",22)
#DEFINE STR0023 FWI18NLang("MATR120","STR0023",23)
#DEFINE STR0024 FWI18NLang("MATR120","STR0024",24)
#DEFINE STR0025 FWI18NLang("MATR120","STR0025",25)
#DEFINE STR0026 FWI18NLang("MATR120","STR0026",26)
#DEFINE STR0027 FWI18NLang("MATR120","STR0027",27)
#DEFINE STR0028 FWI18NLang("MATR120","STR0028",28)
#DEFINE STR0029 FWI18NLang("MATR120","STR0029",29)
#DEFINE STR0030 FWI18NLang("MATR120","STR0030",30)
#DEFINE STR0031 FWI18NLang("MATR120","STR0031",31)
#DEFINE STR0032 FWI18NLang("MATR120","STR0032",32)
#DEFINE STR0033 FWI18NLang("MATR120","STR0033",33)
#DEFINE STR0034 FWI18NLang("MATR120","STR0034",34)
#DEFINE STR0035 FWI18NLang("MATR120","STR0035",35)
#DEFINE STR0036 FWI18NLang("MATR120","STR0036",36)
#DEFINE STR0037 FWI18NLang("MATR120","STR0037",37)
#DEFINE STR0038 FWI18NLang("MATR120","STR0038",38)
#DEFINE STR0039 FWI18NLang("MATR120","STR0039",39)
#DEFINE STR0040 FWI18NLang("MATR120","STR0040",40)
#DEFINE STR0041 FWI18NLang("MATR120","STR0041",41)
#DEFINE STR0042 FWI18NLang("MATR120","STR0042",42)
#DEFINE STR0043 FWI18NLang("MATR120","STR0043",43)
#DEFINE STR0044 FWI18NLang("MATR120","STR0044",44)
#DEFINE STR0045 FWI18NLang("MATR120","STR0045",45)
#DEFINE STR0046 FWI18NLang("MATR120","STR0046",46)
#DEFINE STR0047 FWI18NLang("MATR120","STR0047",47)
#DEFINE STR0048 FWI18NLang("MATR120","STR0048",48)
#DEFINE STR0049 FWI18NLang("MATR120","STR0049",49)
#DEFINE STR0050 FWI18NLang("MATR120","STR0050",50)
#DEFINE STR0051 FWI18NLang("MATR120","STR0051",51)
#DEFINE STR0052 FWI18NLang("MATR120","STR0052",52)
#DEFINE STR0053 FWI18NLang("MATR120","STR0053",53)
#DEFINE STR0054 FWI18NLang("MATR120","STR0054",54)
#DEFINE STR0055 FWI18NLang("MATR120","STR0055",55)
#DEFINE STR0056 FWI18NLang("MATR120","STR0056",56)
#DEFINE STR0057 FWI18NLang("MATR120","STR0057",57)
#DEFINE STR0058 FWI18NLang("MATR120","STR0058",58)
#DEFINE STR0059 FWI18NLang("MATR120","STR0059",59)
#DEFINE STR0060 FWI18NLang("MATR120","STR0060",60)
#DEFINE STR0061 FWI18NLang("MATR120","STR0061",61)
#DEFINE STR0062 FWI18NLang("MATR120","STR0062",62)
#DEFINE STR0063 FWI18NLang("MATR120","STR0063",63)
#DEFINE STR0064 FWI18NLang("MATR120","STR0064",64)
#DEFINE STR0065 FWI18NLang("MATR120","STR0065",65)
#DEFINE STR0066 FWI18NLang("MATR120","STR0066",66)
#DEFINE STR0067 FWI18NLang("MATR120","STR0067",67)
#DEFINE STR0068 FWI18NLang("MATR120","STR0068",68)
#DEFINE STR0069 FWI18NLang("MATR120","STR0069",69)
#DEFINE STR0070 FWI18NLang("MATR120","STR0070",70)
#DEFINE STR0071 FWI18NLang("MATR120","STR0071",71)
#DEFINE STR0072 FWI18NLang("MATR120","STR0072",72)
#DEFINE STR0073 FWI18NLang("MATR120","STR0073",73)
#DEFINE STR0074 FWI18NLang("MATR120","STR0074",74)

/*/{Protheus.doc} User Function ADCOM040R
	Relatório customizado que tem como base o relatório padrão MATR120.
	Chamado 41586.
	@type  Function
	@author Everson
	@since 06/10/2021
	@version 01
	/*/
User Function ADCOM040R() // U_ADCOM040R()
	Local oReport

	oReport := ReportDef()
	oReport:PrintDialog()

	U_ADINF009P(SUBSTRING(ALLTRIM(PROCNAME()),3,LEN(ALLTRIM(PROCNAME()))) + '.PRW',SUBSTRING(ALLTRIM(PROCNAME()),3,LEN(ALLTRIM(PROCNAME()))),'Workflow de previsão de embarque para os processos de exportação')
	
Return

Static Function ReportDef(nReg)

	Local oReport 
	Local oSection1
	Local oSection2 
	Local oSection3 
	Local oSection4
	Local oSection5
	Local oCell         
	Local aOrdem	:= {}
	Local cAliasSC7 := GetNextAlias()

	PRIVATE cPerg := "MTR120"
	//?????????????????????????????????????
	//?riacao do componente de impressao                                      ?
	//?                                                                       ?
	//?Report():New                                                           ?
	//?xpC1 : Nome do relatorio                                               ?
	//?xpC2 : Titulo                                                          ?
	//?xpC3 : Pergunte                                                        ?
	//?xpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ?
	//?xpC5 : Descricao                                                       ?
	//?                                                                       ?
	//?????????????????????????????????????
	oReport:= TReport():New("ADCOM040R",STR0007,"MTR120", {|oReport| ReportPrint(oReport,cAliasSC7)},STR0001+" "+STR0002+" "+STR0003) //"Relacao de Pedidos de Compras"##"Emissao da Relacao de  Pedidos de Compras."##"Sera solicitado em qual Ordem, qual o Intervalo para"##"a emissao dos pedidos de compras."
	oReport:SetLandscape()
	oReport:SetTotalInLine(.F.)
	Pergunte("MTR120",.F.)
	Aadd( aOrdem, STR0004 ) // "Por Numero"
	Aadd( aOrdem, STR0005 ) // "Por Produto"
	Aadd( aOrdem, STR0006 ) // "Por Fornecedor"
	Aadd( aOrdem, STR0049 ) // "Por Previsao de Entrega "

	oSection1 := TRSection():New(oReport,STR0062,{"SC7","SA2","SB1"},aOrdem) //"Relacao de Pedidos de Compras"
	oSection1 :SetTotalInLine(.F.)

	TRCell():New(oSection1,"C7_NUM","SC7",STR0065/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) //"Num.PC"

	TRCell():New(oSection1,"cCompr","   ","Comprador",/*Picture*/,50,/*lPixel*/,{|| ImpComp(cAliasSC7) })

	TRCell():New(oSection1,"C7_FORNECE","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"C7_LOJA","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"A2_NOME","SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"A2_TEL","SA2",/*Titulo*/,/*Picture*/,15,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"A2_FAX","SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"A2_CONTATO","SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"C7_PRODUTO","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)
	TRCell():New(oSection1,"cDescri","   ",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| ImpDescri(cAliasSC7) })
	TRCell():New(oSection1,"C7_DATPRF","SC7",STR0052,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) //"ENTREGA PREVISTA :  "
	oSection1:Cell("cDescri"):GetFieldInfo("B1_DESC")    

	oSection2 := TRSection():New(oSection1,STR0063,{"SC7","SA2","SB1"};
									/* <.lLoadCells.> */ , , /* <cTotalText>  */, /* !<.lTotalInCol.>  */, /* <.lHeaderPage.>  */,;
									/* <.lHeaderBreak.> */, /* <.lPageBreak.>  */, /* <.lLineBreak.>  */, /* <nLeftMargin>  */,;
									/* <.lLineStyle.>  */, /* <nColSpace>  */, /*<.lAutoSize.> */, /*<cSeparator> */,;
									/*<nLinesBefore>  */, /*<nCols>  */, /* <nClrBack> */, /* <nClrFore>  */)
	oSection2 :SetTotalInLine(.F.)
	oSection2 :SetHeaderPage()
	oSection2 :SetTotalText(STR0033) //"Total Geral "

	TRCell():New(oSection2,"C7_NUM","SC7",STR0065/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)//"Num.PC"

	TRCell():New(oSection2,"C7_ITEM","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)
	TRCell():New(oSection2,"C7_NUMSC","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,"RIGHT",,"RIGHT",,,.T.)
	TRCell():New(oSection2,"C7_PRODUTO","SC7",/*Titulo*/,/*Picture*/,,/*lPixel*/,/*{|| code-block de impressao }*/,"CENTER",,"CENTER",,,.T.)
	TRCell():New(oSection2,"cDescri","   ",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| ImpDescri(cAliasSC7) },,,,,,.F.)
	oSection2:Cell("cDescri"):GetFieldInfo("B1_DESC")
	TRCell():New(oSection2,"B1_GRUPO","SB1",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.T.)
	
    TRCell():New(oSection2,"cDsGrp","   ","Desc Grupo",/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| ImpPdGrp(cAliasSC7) },,,,,,.F.)
    
    TRCell():New(oSection2,"C7_EMISSAO","SC7",STR0068/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,"LEFT",,"LEFT",,,.F.)//"Emissao"
	TRCell():New(oSection2,"C7_FORNECE","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,"LEFT",,"LEFT",,,.F.)
	TRCell():New(oSection2,"C7_LOJA","SC7",STR0067/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.F.)//"Lj"
	TRCell():New(oSection2,"A2_NOME","SA2",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,,,,,,.T.)
	TRCell():New(oSection2,"A2_TEL","SA2",/*Titulo*/,/*Picture*/,15,/*lPixel*/,/*{|| code-block de impressao }*/,"CENTER",,"CENTER",,,.T.)
	TRCell():New(oSection2,"C7_DATPRF","SC7",STR0066/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,"CENTER",,"CENTER",,,.T.) //"Entrega"
	TRCell():New(oSection2,"C7_QUANT","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,"RIGHT",,"RIGHT",,,.T.) 
	TRCell():New(oSection2,"C7_UM","SC7",STR0069/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,"CENTER",,"CENTER",,,.F.)//"UM"
	TRCell():New(oSection2,"nVlrUnit","   ",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| ImpVunit(cAliasSC7) },,,,,,.F.)
	oSection2:Cell("nVlrUnit"):GetFieldInfo("C7_PRECO")
	TRCell():New(oSection2,"C7_VLDESC","   ",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| ImpDescon(cAliasSC7) },,,,,,.T.)
	TRCell():New(oSection2,"nVlrIPI","   ",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| ImpVIPI(cAliasSC7) },"RIGHT",,"RIGHT",,,.T.)
	oSection2:Cell("nVlrIPI"):GetFieldInfo("C7_VALIPI")
	TRCell():New(oSection2,"C7_TOTAL","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,{|| ImpVTotal(cAliasSC7) },"RIGHT",,"RIGHT",,,.T.)
	TRCell():New(oSection2,"C7_QUJE","SC7",/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,"CENTER",,"CENTER",,,.F.)
	TRCell():New(oSection2,"nQtdRec","   ",STR0072+CRLF+STR0074,PesqPict('SC7','C7_QUANT'),/*Tamanho*/,/*lPixel*/,{|| If(Empty((cAliasSC7)->C7_RESIDUO),IIF((cAliasSC7)->C7_QUANT-(cAliasSC7)->C7_QUJE<0,0,(cAliasSC7)->C7_QUANT-(cAliasSC7)->C7_QUJE),0) },"CENTER",,"CENTER",,,.F.) //Quant.Receber
	TRCell():New(oSection2,"nSalRec","   ",STR0073+CRLF+STR0074,PesqPict('SC7','C7_TOTAL'),TamSX3("C7_TOTAL")[1],/*lPixel*/,{|| ImpSaldo(cAliasSC7) },"RIGHT",,"RIGHT",,,.T.) //Saldo Receber
	TRCell():New(oSection2,"C7_RESIDUO","   ",STR0070+CRLF+STR0071/*Titulo*/,/*Picture*/,3,/*lPixel*/,{|| If(Empty((cAliasSC7)->C7_RESIDUO),STR0031,STR0032) },"CENTER",,"CENTER",,,.F.) //"Res."##"Elim."

	TRFunction():New(oSection2:Cell("nVlrIPI"),NIL,"SUM",/*oBreak*/,/*Titulo*/,/*cPicture*/,/*uFormula*/,.T.,.T.,,oSection1) 
	TRFunction():New(oSection2:Cell("C7_TOTAL"),NIL,"SUM",/*oBreak*/,/*Titulo*/,/*cPicture*/,/*uFormula*/,.T.,.T.,,oSection1)
	TRFunction():New(oSection2:Cell("nSalRec"),NIL,"SUM",/*oBreak*/,/*Titulo*/,/*cPicture*/,/*uFormula*/,.T.,.T.,,oSection1) 

Return(oReport)

Static Function ReportPrint(oReport,cAliasSC7)

	Local oSection1 := oReport:Section(1) 
	Local oSection2 := oReport:Section(1):Section(1)  
	Local nOrdem    := oReport:Section(1):GetOrder() 
	Local nTamDPro	:= If( TamSX3("C7_PRODUTO")[1] > 15, 20, 30 )
	Local nFilters 	:= 0
	Local nI
	Local cWhere 	:= ""
	Local cFrom 	:= "%%"
	Local lFilUsr	:= oSection1:GetAdvplExp() <> ""

	dbSelectArea("SC7")
	If nOrdem == 4
		dbSetOrder(16) 
	Else
		dbSetOrder(nOrdem)
	EndIf

	If nOrdem == 1
		If ( cPaisLoc$"ARG|POR|EUA" )	//Ordena los pedidos de compra y luego la AE.
			dbSetOrder(10)
		Endif	
		oReport:SetTitle( oReport:Title()+STR0014) // " - POR NUMERO"
		oSection1 :SetTotalText(STR0034) //"Total dos Itens: " 
	ElseIf nOrdem == 2
		oReport:SetTitle( oReport:Title()+STR0018) //" - POR PRODUTO"
		oSection1 :SetTotalText(STR0035) //"Total do Produto"
	ElseIf nOrdem == 3
		oReport:SetTitle( oReport:Title()+STR0022) //" - POR FORNECEDOR"
		oSection1 :SetTotalText(STR0036) //"Total do Fornecedor"
	ElseIf nOrdem == 4
		oReport:SetTitle( oReport:Title()+STR0053) //" - POR PREVISAO DE ENTREGA"
		oSection1 :SetTotalText(STR0043) //"Total da Previsao de Entrega"
	Endif

	If mv_par07==1
		oReport:SetTitle( oReport:Title()+STR0025) //", Todos"
	Elseif mv_par07==2
		oReport:SetTitle( oReport:Title()+STR0026) //", Em Abertos"
	Elseif mv_par07==3
		oReport:SetTitle( oReport:Title()+STR0027) //", Residuos"
	Elseif mv_par07==4
		oReport:SetTitle( oReport:Title()+STR0028) //", Atendidos"
	Elseif mv_par07==5
		oReport:SetTitle( oReport:Title()+STR0059) //", Atendidos + Parcial entregue"
	Endif
	oReport:SetTitle( oReport:Title()+" - " + GetMv("MV_MOEDA"+STR(mv_par13,1))) //" MOEDA "
	//?????????????????????????????????????
	//?iltragem do relat?io                                                  ?
	//?????????????????????????????????????

		//?????????????????????????????????????
		//?ransforma parametros Range em expressao SQL                            ?
		//?????????????????????????????????????
		MakeSqlExpr(oReport:uParam)
		//?????????????????????????????????????
		//?uery do relat?io da secao 1                                           ?
		//?????????????????????????????????????
		oReport:Section(1):BeginQuery()	

		cWhere :="%"

		If mv_par11 == 1 
			cWhere += "AND C7_CONAPRO <> 'B' "	
		ElseIf mv_par11 == 2
			cWhere += "AND C7_CONAPRO = 'B' "	
		Endif

		If mv_par07 == 2 
			cWhere += "AND ( (C7_QUANT-C7_QUJE) > 0 ) "	
			cWhere += "AND C7_RESIDUO = ' ' "
		ElseIf mv_par07 == 3
			cWhere += "AND C7_RESIDUO <> ' ' "
		ElseIf mv_par07 == 4
			cWhere += "AND C7_QUANT <= C7_QUJE "	
		ElseIf mv_par07 == 5
			cWhere += "AND C7_QUJE > 0 "		
		Endif
		
		If mv_par10 == 1 
			cWhere += "AND C7_TIPO = 1 "	
		ElseIf mv_par10 == 2
			cWhere += "AND C7_TIPO = 2 "	
		Endif
		
		If mv_par12 == 1 //Firmes
			cWhere += "AND (C7_TPOP = 'F' OR C7_TPOP = ' ') "	
		ElseIf mv_par12 == 2 //Previstas
			cWhere += "AND C7_TPOP = 'P' "	
		Endif
		
		If lFilUsr
			nFilters := len(oSection1:aUserFilter)
			
			cFrom := "%"
			
			For nI := 1 to nFilters
				If oSection1:aUserFilter[nI][1] == "SA2" .And. oSection1:aUserFilter[nI][2] <> ""
					cFrom += "," + RetSqlName("SA2") + " SA2 "
					cWhere += "AND C7_FORNECE = A2_COD AND SA2.D_E_L_E_T_ = ' ' "
					If FWModeAccess("SC7")=="E" .And. FWModeAccess("SA2")=="E"
						cWhere += "AND SC7.C7_FILIAL = SA2.A2_FILIAL "
					Endif
					
				ElseIf	oSection1:aUserFilter[nI][1] == "SB1" .And. oSection1:aUserFilter[nI][2] <> ""
					cFrom += "," + RetSqlName("SB1") + " SB1 "
					cWhere += "AND C7_PRODUTO = B1_COD AND SB1.D_E_L_E_T_ = ' ' "
					If FWModeAccess("SC7")=="E" .And. FWModeAccess("SB1")=="E"
						cWhere += "AND SC7.C7_FILIAL = SB1.B1_FILIAL "
					Endif
				Endif	
			Next nI
			
			cFrom += "%" 
		Endif

		cWhere +="%"	

		BeginSql Alias cAliasSC7

		SELECT SC7.C7_NUM, SC7.C7_ITEM, SC7.C7_FORNECE, SC7.C7_LOJA, SC7.C7_DATPRF, SC7.C7_EMISSAO,;
			SC7.C7_PRODUTO, SC7.C7_NUMSC, SC7.C7_QUANT, SC7.C7_UM, SC7.C7_VLDESC, SC7.C7_TOTAL,;
			SC7.C7_QUJE, SC7.C7_RESIDUO,SC7.C7_DESCRI, SC7.C7_TXMOEDA, SC7.C7_REAJUST, SC7.C7_PRECO,;
			SC7.C7_MOEDA, SC7.C7_DESC1, SC7.C7_DESC2, SC7.C7_DESC3, SC7.C7_IPIBRUT, SC7.C7_IPI,;
			SC7.C7_SEQUEN, SC7.C7_USER
		
		FROM %table:SC7% SC7 %Exp:cFrom%
		
		WHERE C7_FILIAL = %xFilial:SC7% AND 
			C7_NUM >= %Exp:mv_par08% AND 
			C7_NUM <= %Exp:mv_par09% AND 	 	  
			C7_PRODUTO >= %Exp:mv_par01% AND 
			C7_PRODUTO <= %Exp:mv_par02% AND 
			C7_EMISSAO >= %Exp:Dtos(mv_par03)% AND 
			C7_EMISSAO <= %Exp:Dtos(mv_par04)% AND 
			C7_DATPRF >= %Exp:Dtos(mv_par05)% AND 
			C7_DATPRF <= %Exp:Dtos(mv_par06)% AND 		  
			C7_FORNECE >= %Exp:mv_par15% AND 
			C7_FORNECE <= %Exp:mv_par16% AND 
			SC7.%NotDel% 
			%Exp:cWhere%
			
		ORDER BY %Order:SC7% 
				
		EndSql 
		//?????????????????????????????????????
		//?etodo EndQuery ( Classe TRSection )                                    ?
		//?                                                                       ?
		//?repara o relat?io para executar o Embedded SQL.                       ?
		//?                                                                       ?
		//?xpA1 : Array com os parametros do tipo Range                           ?
		//?                                                                       ?
		//?????????????????????????????????????
		oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)

	oSection2:SetParentQuery()

	Do Case
		Case nOrdem == 1
			oSection2:SetParentFilter( { |cParam| (cAliasSC7)->C7_NUM == cParam },{ || (cAliasSC7)->C7_NUM })
		Case nOrdem == 2
			oSection2:SetParentFilter( { |cParam| (cAliasSC7)->C7_PRODUTO == cParam },{ || (cAliasSC7)->C7_PRODUTO })
		Case nOrdem == 3
			oSection2:SetParentFilter( { |cParam| (cAliasSC7)->C7_FORNECE+(cAliasSC7)->C7_LOJA == cParam },{ || (cAliasSC7)->C7_FORNECE+(cAliasSC7)->C7_LOJA })
		Case nOrdem == 4
			oSection2:SetParentFilter( { |cParam| (cAliasSC7)->C7_DATPRF == cParam },{ || (cAliasSC7)->C7_DATPRF })
	EndCase
	//?????????????????????????????????????
	//?etodo TrPosition()                                                     ?
	//?                                                                       ?
	//?osiciona em um registro de uma outra tabela. O posicionamento ser?    ?
	//?ealizado antes da impressao de cada linha do relat?io.                ?
	//?                                                                       ?
	//?                                                                       ?
	//?xpO1 : Objeto Report da Secao                                          ?
	//?xpC2 : Alias da Tabela                                                 ?
	//?xpX3 : Ordem ou NickName de pesquisa                                   ?
	//?xpX4 : String ou Bloco de c?igo para pesquisa. A string ser?macroexe-?
	//?       cutada.                                                         ?
	//?                                                                       ?			
	//?????????????????????????????????????
	TRPosition():New(oSection1,"SA2",1,{|| xFilial("SA2") + (cAliasSC7)->C7_FORNECE+(cAliasSC7)->C7_LOJA})
	TRPosition():New(oSection1,"SB1",1,{|| xFilial("SB1") + (cAliasSC7)->C7_PRODUTO})
	TRPosition():New(oSection2,"SA2",1,{|| xFilial("SA2") + (cAliasSC7)->C7_FORNECE+(cAliasSC7)->C7_LOJA})
	TRPosition():New(oSection2,"SB1",1,{|| xFilial("SB1") + (cAliasSC7)->C7_PRODUTO})
	//?????????????????????????????????????
	//?nicio da impressao do fluxo do relat?io                               ?
	//?????????????????????????????????????
	oReport:SetMeter(SC7->(LastRec()))

	If nOrdem <> 2
		oSection1:Cell("cDescri"):SetSize(nTamDPro)
		oSection2:Cell("cDescri"):SetSize(nTamDPro)
	EndIf

	Do Case
			Case nOrdem == 1

				oSection1:Cell("C7_PRODUTO"):Disable()
				oSection1:Cell("cDescri"):Disable()
				oSection1:Cell("A2_FAX"):Disable()
				oSection1:Cell("A2_CONTATO"):Disable()
				oSection1:Cell("C7_DATPRF"):Disable()

				oSection2:Cell("C7_NUM"):Disable()
				oSection2:Cell("C7_FORNECE"):Disable()
				oSection2:Cell("A2_NOME"):Disable()
				oSection2:Cell("A2_TEL"):Disable()
				
				oSection1:Print()
			
			Case nOrdem == 2

				oSection1:Cell("C7_NUM"):Disable()
				oSection1:Cell("C7_FORNECE"):Disable()
				oSection1:Cell("A2_NOME"):Disable()
				oSection1:Cell("A2_TEL"):Disable()
				oSection1:Cell("A2_FAX"):Disable()
				oSection1:Cell("A2_CONTATO"):Disable()
				oSection1:Cell("C7_DATPRF"):Disable()
				
				oSection2:Cell("C7_PRODUTO"):Disable()
				oSection2:Cell("cDescri"):Disable()
				oSection2:Cell("B1_GRUPO"):Disable()
				oSection2:Cell("C7_UM"):Disable()
				oSection2:Cell("A2_TEL"):Disable()
				
				oSection1:Print()
		
			Case nOrdem == 3

				oSection1:Cell("C7_NUM"):Disable()
				oSection1:Cell("C7_PRODUTO"):Disable()
				oSection1:Cell("cDescri"):Disable()
				oSection1:Cell("C7_DATPRF"):Disable()
				
				oSection2:Cell("C7_FORNECE"):Disable()
				oSection2:Cell("A2_NOME"):Disable()
				oSection2:Cell("A2_TEL"):Disable()
				oSection2:Cell("C7_UM"):Disable()

				oSection1:Print()
				
			Case nOrdem == 4
			
				oSection1:Cell("C7_NUM"):Disable()
				oSection1:Cell("C7_FORNECE"):Disable()
				oSection1:Cell("A2_NOME"):Disable()
				oSection1:Cell("A2_TEL"):Disable()
				oSection1:Cell("A2_FAX"):Disable()
				oSection1:Cell("A2_CONTATO"):Disable()
				oSection1:Cell("C7_PRODUTO"):Disable() 
				oSection1:Cell("cDescri"):Disable()
				
				oSection2:Cell("B1_GRUPO"):Disable()
				oSection2:Cell("C7_DATPRF"):Disable()
				oSection2:Cell("C7_UM"):Disable()
				oSection2:Cell("C7_FORNECE"):SetTitle(STR0064) //"Fornec."
				oSection2:Cell("cDescri"):SetSize(15)            

				If TamSX3("C7_PRODUTO")[1] > 15
					oSection2:Cell("A2_NOME"):Disable()
					oSection1:Cell("cDescri"):SetSize(14)
					oSection2:Cell("cDescri"):SetSize(14)
				Else
					oSection1:Cell("A2_NOME"):SetSize(15)
					oSection2:Cell("A2_NOME"):SetSize(15)
				EndIf
			
				oSection1:Print()			
	EndCase			

Return NIL

Static Function ImpDescri(cAliasSC7)

	Local aArea   := GetArea()
	Local cDescri := ""

	If Empty(mv_par14)
		mv_par14 := "B1_DESC"
	EndIf

	//????????????????????????????????
	//?Impressao da descricao cientifica do Produto.                ?
	//????????????????????????????????
	If AllTrim(mv_par14) == "B5_CEME"
		dbSelectArea("SB5")
		dbSetOrder(1)
		dbSeek( xFilial("SB5")+(cAliasSC7)->C7_PRODUTO )
		cDescri := Alltrim(B5_CEME)
	EndIf

	If AllTrim(mv_par14) == "C7_DESCRI"
		dbSelectArea("SC7")
		cDescri := Alltrim((cAliasSC7)->C7_DESCRI)
	EndIf

	//????????????????????????????????
	//?Impressao da descricao generica do Produto.                  ?
	//????????????????????????????????
	If AllTrim(mv_par14) == "B1_DESC" .OR. Empty(cDescri)
		dbSelectArea("SB1")
		dbSetOrder(1)
		dbSeek( xFilial("SB1")+(cAliasSC7)->C7_PRODUTO )
		cDescri := Alltrim(SB1->B1_DESC)
	EndIf

	RestArea(aArea)
Return(cDescri)

Static Function ImpVunit(cAliasSC7)

	Local aArea    := GetArea()
	Local nVlrUnit := 0
	Local aTam	   := TamSx3("C7_PRECO")
	Local nTxMoeda := IIF((cAliasSC7)->C7_TXMOEDA > 0,(cAliasSC7)->C7_TXMOEDA,Nil)

	If !Empty((cAliasSC7)->C7_REAJUST)
		nVlrUnit := xMoeda(ADCOM401((cAliasSC7)->C7_REAJUST),(cAliasSC7)->C7_MOEDA,mv_par13,(cAliasSC7)->C7_DATPRF,aTam[2],nTxMoeda) 
	Else
		nVlrUnit := xMoeda((cAliasSC7)->C7_PRECO,(cAliasSC7)->C7_MOEDA,mv_par13,(cAliasSC7)->C7_DATPRF,aTam[2],nTxMoeda) 
	Endif

	RestArea(aArea)

Return(nVlrUnit)

Static Function ImpVIPI(cAliasSC7)

	Local aArea    := GetArea()
	Local nVlrIPI  := 0
	Local nToTIPI  := 0
	Local nTotal   := 0
	Local nItemIVA := 0 
	Local nValor   := ((cAliasSC7)->C7_QUANT) * IIf(Empty((cAliasSC7)->C7_REAJUST),(cAliasSC7)->C7_PRECO,Formula((cAliasSC7)->C7_REAJUST))
	Local nTotDesc := (cAliasSC7)->C7_VLDESC
	Local nTxMoeda := IIF((cAliasSC7)->C7_TXMOEDA > 0,(cAliasSC7)->C7_TXMOEDA,Nil)
	Local nI 

	If cPaisLoc <> "BRA"
		R120FIniPC((cAliasSC7)->C7_NUM,(cAliasSC7)->C7_ITEM,(cAliasSC7)->C7_SEQUEN)
		aValIVA := MaFisRet(,"NF_VALIMP")
		If !Empty( aValIVA )
			For nI := 1 To Len( aValIVA )
				nItemIVA += aValIVA[nI]
			Next
		Endif
		nVlrIPI := xMoeda(nItemIVA,(cAliasSC7)->C7_MOEDA,mv_par13,(cAliasSC7)->C7_DATPRF,,nTxMoeda)  
	Else
		If nTotDesc == 0
			nTotDesc := CalcDesc(nValor,(cAliasSC7)->C7_DESC1,(cAliasSC7)->C7_DESC2,(cAliasSC7)->C7_DESC3)
		EndIF
		nTotal := nValor - nTotDesc
		nTotIPI := IIF((cAliasSC7)->C7_IPIBRUT == "L",nTotal, nValor) * ( (cAliasSC7)->C7_IPI / 100 )
		nVlrIPI := xMoeda(nTotIPI,(cAliasSC7)->C7_MOEDA,mv_par13,(cAliasSC7)->C7_DATPRF,,nTxMoeda)  
	EndIf

	RestArea(aArea)

Return(nVlrIPI)

Static Function ImpSaldo(cAliasSC7)

	Local aArea    := GetArea()
	Local nSalRec  := 0
	Local nItemIVA := 0 
	Local nQuant   := If(Empty((cAliasSC7)->C7_RESIDUO),IIF((cAliasSC7)->C7_QUANT-(cAliasSC7)->C7_QUJE<0,0,(cAliasSC7)->C7_QUANT-(cAliasSC7)->C7_QUJE),(cAliasSC7)->C7_QUANT-((cAliasSC7)->C7_QUANT-(cAliasSC7)->C7_QUJE))
	Local nTotal   := 0
	Local nSalIPI  := 0
	Local nValor   := ((cAliasSC7)->C7_QUANT) * IIf(Empty((cAliasSC7)->C7_REAJUST),(cAliasSC7)->C7_PRECO,Formula((cAliasSC7)->C7_REAJUST))
	Local nSaldo   := IIF((cAliasSC7)->C7_QUANT-(cAliasSC7)->C7_QUJE=0,(cAliasSC7)->C7_QUJE,(cAliasSC7)->C7_QUANT-(cAliasSC7)->C7_QUJE)* IIf(Empty((cAliasSC7)->C7_REAJUST),(cAliasSC7)->C7_PRECO,Formula((cAliasSC7)->C7_REAJUST))
	Local nTotDesc := (cAliasSC7)->C7_VLDESC
	Local nTxMoeda := IIF((cAliasSC7)->C7_TXMOEDA > 0,(cAliasSC7)->C7_TXMOEDA,Nil)
	Local nI 

	If cPaisLoc <> "BRA"
		R120FIniPC((cAliasSC7)->C7_NUM,(cAliasSC7)->C7_ITEM,(cAliasSC7)->C7_SEQUEN)
		aValIVA  := MaFisRet(,"NF_VALIMP")
		If !Empty( aValIVA )
			For nI := 1 To Len( aValIVA )
				nItemIVA += aValIVA[nI]
			Next
		Endif
		nSalIPI := ((cAliasSC7)->C7_QUANT-(cAliasSC7)->C7_QUJE) * nItemIVA / (cAliasSC7)->C7_QUANT
	Else
		If nTotDesc == 0
			nTotDesc := CalcDesc(nValor,(cAliasSC7)->C7_DESC1,(cAliasSC7)->C7_DESC2,(cAliasSC7)->C7_DESC3)
		EndIF
		nTotal := nValor - nTotDesc
		If Empty((cAliasSC7)->C7_RESIDUO)
			nTotal := nSaldo - nTotDesc
			nSalIPI := IIF((cAliasSC7)->C7_IPIBRUT == "L",nTotal, nSaldo) * ( (cAliasSC7)->C7_IPI / 100 )
		Endif
	EndIf 

	If Empty((cAliasSC7)->C7_REAJUST) 
		nSalRec := IIf(nQuant < 0,(cAliasSC7)->C7_QUANT,nQuant) * xMoeda((cAliasSC7)->C7_PRECO,(cAliasSC7)->C7_MOEDA,mv_par13,(cAliasSC7)->C7_DATPRF,TamSX3("C7_PRECO")[2],nTxMoeda) - xMoeda((cAliasSC7)->C7_VLDESC,(cAliasSC7)->C7_MOEDA,mv_par13,(cAliasSC7)->C7_DATPRF,TamSX3("C7_VLDESC")[2],nTxMoeda)
		If nSalRec > 0
			nSalRec := nSalRec + xMoeda(nSalIPI,(cAliasSC7)->C7_MOEDA,mv_par13,(cAliasSC7)->C7_DATPRF,,nTxMoeda)
		Endif
	Else   
		nSalRec  := nQuant * xMoeda(Formula((cAliasSC7)->C7_REAJUST),(cAliasSC7)->C7_MOEDA,mv_par13,(cAliasSC7)->C7_DATPRF,,nTxMoeda)
		If nSalRec > 0
			nSalRec := nSalRec + xMoeda(nSalIPI,(cAliasSC7)->C7_MOEDA,mv_par13,(cAliasSC7)->C7_DATPRF,,nTxMoeda)
		Endif
	Endif 

	RestArea(aArea) 

Return(nSalRec) 

Static Function ImpVTotal(cAliasSC7)

	Local aArea    := GetArea()
	Local nVlrTot  := 0
	Local nTxMoeda := IIF((cAliasSC7)->C7_TXMOEDA > 0,(cAliasSC7)->C7_TXMOEDA,Nil)

	R120FIniPC((cAliasSC7)->C7_NUM,(cAliasSC7)->C7_ITEM,(cAliasSC7)->C7_SEQUEN)
	nTotal  := MaFisRet(,'NF_TOTAL')
	nVlrTot := xMoeda(nTotal,(cAliasSC7)->C7_MOEDA,mv_par13,(cAliasSC7)->C7_DATPRF,,nTxMoeda)		  		

	RestArea(aArea)

Return(nVlrTot)

Static Function ImpDescon(cAliasSC7)

	Local aArea    := GetArea()
	Local nVlrDesc := 0
	Local nTxMoeda := IIF((cAliasSC7)->C7_TXMOEDA > 0,(cAliasSC7)->C7_TXMOEDA,Nil)

	nVlrDesc := xMoeda(C7_VLDESC,SC7->C7_MOEDA,mv_par13,SC7->C7_DATPRF,,nTxMoeda)

	RestArea(aArea)

Return(nVlrDesc)

User FUNCTION ADCOM401(cFormula)
	Local xAlias 
	Local cForm		:= "" , xValor
	Local cString 	:= "SC7->"
	Local cNewAlias := "" 
	Local cNewForm	:= ""
	//????????????????????????????????????
	//?alva a integridade dos dados                                         ?
	//????????????????????????????????????
	xAlias := Alias()

	DbSelectArea("SM4")
	If DbSeek(cFilial+cFormula)
		cForm := AllTrim(M4_FORMULA)
		cNewAlias := xAlias + "->"
		cNewForm := StrTran(cForm,cString,cNewAlias)
		DbSelectArea(xAlias)      
		xValor := &cNewForm
	Else
		xValor := NIL
	EndIf
	DbSelectArea(xAlias)
Return xValor

Static Function R120FIniPC(cPedido,cItem,cSequen,cFiltro)

	Local aArea		:= GetArea()
	Local aAreaSC7	:= SC7->(GetArea())
	Local cValid	:= ""
	Local nPosRef	:= 0
	Local nItem		:= 0
	Local cItemDe	:= IIf(cItem==Nil,'',cItem)
	Local cItemAte	:= IIf(cItem==Nil,Repl('Z',Len(SC7->C7_ITEM)),cItem)
	Local cRefCols	:= ''
	Local aStru		:= FWFormStruct(3,"SC7")[1]
	Local nX

	DEFAULT cSequen	:= ""
	DEFAULT cFiltro	:= ""

	dbSelectArea("SC7")
	dbSetOrder(1)
	If dbSeek(xFilial("SC7")+cPedido+cItemDe+Alltrim(cSequen))
		MaFisEnd()
		MaFisIni(SC7->C7_FORNECE,SC7->C7_LOJA,"F","N","R",{})
		While !Eof() .AND. SC7->C7_FILIAL+SC7->C7_NUM == xFilial("SC7")+cPedido .AND. ;
				SC7->C7_ITEM <= cItemAte .AND. (Empty(cSequen) .OR. cSequen == SC7->C7_SEQUEN)

			// Nao processar os Impostos se o item possuir residuo eliminado  
			If &cFiltro
				dbSelectArea('SC7')
				dbSkip()
				Loop
			EndIf
				
			// Inicia a Carga do item nas funcoes MATXFIS  
			nItem++
			MaFisIniLoad(nItem)

			For nX := 1 To Len(aStru)
				cValid	:= StrTran(UPPER(GetCbSource(aStru[nX][7]))," ","")
				cValid	:= StrTran(cValid,"'",'"')
				If "MAFISREF" $ cValid .And. !(aStru[nX][14])
					nPosRef  := AT('MAFISREF("',cValid) + 10
					cRefCols := Substr(cValid,nPosRef,AT('","MT120",',cValid)-nPosRef )
					// Carrega os valores direto do SC7.           
					MaFisLoad(cRefCols,&("SC7->"+ aStru[nX][3]),nItem)
				EndIf
			Next nX

			MaFisEndLoad(nItem,2)
			dbSelectArea('SC7')
			dbSkip()
		End
	EndIf

	RestArea(aAreaSC7)
	RestArea(aArea)

Return .T.

Static Function ImpComp(cAliasSC7)
Return Alltrim(cValToChar(Posicione("SY1",3,FWxFilial("SY1") + (cAliasSC7)->C7_USER,"Y1_NOME")))

Static Function ImpPdGrp(cAliasSC7)

	//Variáveis.
	Local cGrp   := Posicione("SB1",1,FWxFilial("SB1") + (cAliasSC7)->C7_PRODUTO,"B1_GRUPO")

Return Alltrim(cValToChar(Posicione("SBM",1,FWxFilial("SBM") + cGrp ,"BM_DESC")))
